/*
 * Copyright 2014-2017 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'nebula.plugin-plugin' version '8.1.3'
    id 'org.ysb33r.gradletest' version '0.5.4'
    id 'java-gradle-plugin'
}

description 'Gradle plugin to ease use of ivy-publish and maven-publish'
group = 'com.netflix.nebula'

contacts {
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile 'com.netflix.nebula:gradle-info-plugin:latest.release', optional
    compile 'com.netflix.nebula:gradle-contacts-plugin:latest.release', optional

    testCompile gradleTestKit()
    testCompile 'com.netflix.nebula:gradle-dependency-lock-plugin:latest.release'
    testCompile 'com.netflix.nebula:gradle-resolution-rules-plugin:latest.release'
    testCompile 'com.netflix.nebula:nebula-dependency-recommender:latest.release'
    testCompile('org.jfrog.buildinfo:build-info-extractor-gradle:latest.release') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testRuntime files(tasks.createClasspathManifest)
}

gradlePlugin {
    plugins {
        mavenApacheLicense {
            id = 'nebula.maven-apache-license'
            implementationClass = 'nebula.plugin.publishing.maven.license.MavenApacheLicensePlugin'
        }

        mavenBasePublish {
            id = 'nebula.maven-base-publish'
            implementationClass = 'nebula.plugin.publishing.maven.MavenBasePublishPlugin'
        }

        mavenCompileOnly {
            id = 'nebula.maven-compile-only'
            implementationClass = 'nebula.plugin.publishing.maven.MavenCompileOnlyPlugin'
        }

        mavenDeveloper {
            id = 'nebula.maven-developer'
            implementationClass = 'nebula.plugin.publishing.maven.MavenDeveloperPlugin'
        }

        mavenManifest {
            id = 'nebula.maven-manifest'
            implementationClass = 'nebula.plugin.publishing.maven.MavenManifestPlugin'
        }

        mavenNebulaPublish {
            id = 'nebula.maven-nebula-publish'
            implementationClass = 'nebula.plugin.publishing.maven.MavenNebulaPublishPlugin'
        }

        mavenPublish {
            id = 'nebula.maven-publish'
            implementationClass = 'nebula.plugin.publishing.maven.MavenPublishPlugin'
        }

        mavenResolvedDependencies {
            id = 'nebula.maven-resolved-dependencies'
            implementationClass = 'nebula.plugin.publishing.maven.MavenResolvedDependenciesPlugin'
        }

        mavenScm {
            id = 'nebula.maven-scm'
            implementationClass = 'nebula.plugin.publishing.maven.MavenScmPlugin'
        }

        ivyBasePublish {
            id = 'nebula.ivy-base-publish'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyBasePublishPlugin'
        }

        ivyCompileOnly {
            id = 'nebula.ivy-compile-only'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyCompileOnlyPlugin'
        }

        ivyManifest {
            id = 'nebula.ivy-manifest'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyManifestPlugin'
        }

        ivyNebulaPublish {
            id = 'nebula.ivy-nebula-publish'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyNebulaPublishPlugin'
        }

        ivyPublish {
            id = 'nebula.ivy-publish'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyPublishPlugin'
        }

        ivyResolvedDependencies {
            id = 'nebula.ivy-resolved-dependencies'
            implementationClass = 'nebula.plugin.publishing.ivy.IvyResolvedDependenciesPlugin'
        }

        publishVerification {
            id = 'nebula.publish-verification'
            implementationClass = 'nebula.plugin.publishing.verification.PublishVerificationPlugin'
        }

        compileApi {
            id = 'nebula.compile-api'
            implementationClass = 'nebula.plugin.publishing.scopes.ApiScopePlugin'
        }

        javadocJar {
            id = 'nebula.javadoc-jar'
            implementationClass = 'nebula.plugin.publishing.publications.JavadocJarPlugin'
        }

        sourceJar {
            id = 'nebula.source-jar'
            implementationClass = 'nebula.plugin.publishing.publications.SourceJarPlugin'
        }

        testJar {
            id = 'nebula.test-jar'
            implementationClass = 'nebula.plugin.publishing.publications.TestJarPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-publishing-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-publishing-plugin.git'
    description = 'Plugins to ease maven-publish and ivy-publish configuration'

    plugins {
        mavenApacheLicense {
            id = 'nebula.maven-apache-license'
            displayName = 'Nebula Maven Apache License Plugin'
            description = 'Add the Apache v2 license to the pom'
            tags = ['nebula', 'publish', 'maven', 'apache', 'license']
        }

        mavenBasePublish {
            id = 'nebula.maven-base-publish'
            displayName = 'Nebula Maven Base Publish Plugin'
            description = 'Setup nebula maven publication. Add a name and description element to the published POM'
            tags = ['nebula', 'publish', 'maven']
        }

        mavenCompileOnly {
            id = 'nebula.maven-compile-only'
            displayName = 'Nebula Maven CompileOnly Plugin'
            description = 'Deal with compileOnly configuration'
            tags = ['nebula', 'publish', 'maven']
        }

        mavenManifest {
            id = 'nebula.maven-manifest'
            displayName = 'Nebula Maven Manifest Plugin'
            description = 'Append additional information from our info plugin to the properties section of the pom'
            tags = ['nebula', 'publish', 'maven']
        }

        mavenPublish {
            id = 'nebula.maven-publish'
            displayName = 'Nebula Maven Publish Plugin'
            description = 'Applies our opinions for maven publications'
            tags = ['nebula', 'publish', 'maven']
        }

        mavenResolvedDependencies {
            id = 'nebula.maven-resolved-dependencies'
            displayName = 'Nebula Maven Resolved Dependencies Plugin'
            description = 'Built on top of nebula.maven-dependencies, replacing dynamic versions with the actual resolved versions'
            tags = ['nebula', 'publish', 'maven']
        }

        mavenScm {
            id = 'nebula.maven-scm'
            displayName = 'Nebula Maven SCM Plugin'
            description = 'Adds SCM info to the pom'
            tags = ['nebula', 'publish', 'maven']
        }

        ivyBasePublish {
            id = 'nebula.ivy-base-publish'
            displayName = 'Nebula Ivy Base Publish Plugin'
            description = 'Setup nebulaIvy IvyPublication'
            tags = ['nebula', 'publish', 'ivy']
        }

        ivyCompileOnly {
            id = 'nebula.ivy-compile-only'
            displayName = 'Nebula Ivy CompileOnly Plugin'
            description = 'Deal with compileOnly configuration'
            tags = ['nebula', 'publish', 'ivy']
        }

        ivyManifest {
            id = 'nebula.ivy-manifest'
            displayName = 'Nebula Ivy Manifest Plugin'
            description = 'Append additional information from our info plugin to the info section of the ivy.xml'
            tags = ['nebula', 'publish', 'ivy']
        }

        ivyPublish {
            id = 'nebula.ivy-publish'
            displayName = 'Nebula Ivy Publish Plugin'
            description = 'Applies our opinions for ivy publications'
            tags = ['nebula', 'publish', 'ivy']
        }

        ivyResolvedDependencies {
            id = 'nebula.ivy-resolved-dependencies'
            displayName = 'Nebula Ivy Resolved Dependencies Plugin'
            description = 'Built on top of nebula.ivy-dependencies, replacing dynamic versions with the actual resolved versions'
            tags = ['nebula', 'publish', 'ivy']
        }

        compileApi {
            id = 'nebula.compile-api'
            displayName = 'Nebula compileApi Scope Plugin'
            description = 'Adds a compileApi configuration, dependencies in this configuration will be put in compile conf/scope of ivy/maven'
            tags = ['nebula', 'publish', 'api-scope', 'compileApi']
        }

        javadocJar {
            id = 'nebula.javadoc-jar'
            displayName = 'Nebula Javadoc Jar Publishing plugin'
            description = 'Create a javadocJar task to package up the javadoc and add it to the publications'
            tags = ['nebula', 'publish', 'javadoc']
        }

        sourceJar {
            id = 'nebula.source-jar'
            displayName = 'Nebula Source Jar Publishing plugin'
            description = 'Create a sourceJar task to package up the project\'s source code and add it to the publications'
            tags = ['nebula', 'publish', 'source']
        }

        testJar {
            id = 'nebula.test-jar'
            displayName = 'Nebula Test Jar Publishing plugin'
            description = 'Create a testJar task to package up test classes and add it to the publications'
            tags = ['nebula', 'publish', 'test']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-publishing-plugin'
    }
}

gradleTest {
    versions '2.13', '3.4', '3.5.1', '4.1'
}

gradleTest.dependsOn(jar)
gradleTest.doFirst {
    project.version = project.version.toString()
}
