import org.gradle.api.publish.maven.MavenPublication

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3' }
}

repositories { jcenter() }

apply plugin: 'groovy'
apply plugin: 'idea'

group = 'com.netflix.nebula'

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

apply plugin: 'bintray'
def packageName = name
bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenJava']
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'nebula'
        name = packageName
        licenses = ['Apache-2.0']
    }
    // Will have to manually publish on bintray.com
}
bintrayUpload.dependsOn(build)

task install(dependsOn: 'publishMavenJavaPublicationToMavenLocal') << {
    logger.info "Installed $project.name"
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.netflix.nebula:nebula-core:1.12.0'
    testCompile 'com.netflix.nebula:nebula-test:1.12.0'

    compile('org.semver:api:0.9.18') {
        exclude group: 'asm', module: 'asm-commons'
        exclude group: 'asm', module: 'asm'
        exclude group: 'de.tototec', module: 'de.tototec.cmdoption'
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.12-rc-2'
}
